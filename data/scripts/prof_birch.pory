script ProfBirch_EventScript_UpdateLocation {
    if (flag(FLAG_SYS_GAME_CLEAR) || var(VAR_BIRCH_STATE) == 0 ||
        var(VAR_BIRCH_STATE) == 1 || var(VAR_BIRCH_STATE) == 6 ||
        var(VAR_BIRCH_STATE) == 7)
    {
        // Move to Lab.
        clearflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_BIRCH)
        clearflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_UNKNOWN_0x380)
        setflag(FLAG_HIDE_ROUTE_101_BIRCH)
        setflag(FLAG_HIDE_ROUTE_103_BIRCH)
    } elif (var(VAR_BIRCH_STATE) == 2 || var(VAR_BIRCH_STATE) == 3) {
        // Move to Route 101.
        clearflag(FLAG_HIDE_ROUTE_101_BIRCH)
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_BIRCH)
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_UNKNOWN_0x380)
        setflag(FLAG_HIDE_ROUTE_103_BIRCH)
    } else {
        // Move to Route 103.
        clearflag(FLAG_HIDE_ROUTE_103_BIRCH)
        setflag(FLAG_HIDE_ROUTE_101_BIRCH)
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_BIRCH)
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_UNKNOWN_0x380)
    }
}

script ProfBirch_EventScript_RatePokedexOrRegister {
    lock
    faceplayer
    if (flag(FLAG_HAS_MATCH_CALL) && !flag(FLAG_ENABLE_PROF_BIRCH_MATCH_CALL)) {
        goto(EventScript_RegisterProfBirch)
    } else {
        msgbox(gBirchDexRatingText_AreYouCurious, MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO) {
            msgbox(gBirchDexRatingText_Cancel)
        } else {
            call(ProfBirch_EventScript_RatePokedex)
        }
    }
    release
}

script ProfBirch_EventScript_RatePokedex {
    setvar(VAR_0x8004, 0)
    specialvar(VAR_RESULT, ScriptGetPokedexInfo)
    copyvar(VAR_0x8008, VAR_0x8005)
    copyvar(VAR_0x8009, VAR_0x8006)
    copyvar(VAR_0x800A, VAR_RESULT)
    buffernumberstring(0, VAR_0x8008) // Num Hoenn seen
    buffernumberstring(1, VAR_0x8009) // Num Hoenn caught
    msgbox(gBirchDexRatingText_SoYouveSeenAndCaught)
    copyvar(VAR_0x8004, VAR_0x8009)
    special(ShowPokedexRatingMessage)
    waitmessage
    waitbuttonpress
    if (var(VAR_0x800A) != 0) {
        setvar(VAR_0x8004, 1)
        specialvar(VAR_RESULT, ScriptGetPokedexInfo)
        copyvar(VAR_0x8008, VAR_0x8005)
        copyvar(VAR_0x8009, VAR_0x8006)
        buffernumberstring(0, VAR_0x8008) // Num National seen
        buffernumberstring(1, VAR_0x8009) // Num National caught
        msgbox(gBirchDexRatingText_OnANationwideBasis)
    }
}
