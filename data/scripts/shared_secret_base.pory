mapscripts SecretBase_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_SECRET_BASE_INITIALIZED, 0: SecretBase_EventScript_InitDecorations
    ]
    MAP_SCRIPT_ON_TRANSITION {
        call(SecretBase_EventScript_SetDecorationFlags)
        special(SetSecretBaseOwnerGfxId)
        special(InitSecretBaseVars)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_INIT_SECRET_BASE, 0: SecretBase_EventScript_FirstEntrance
    ]
    MAP_SCRIPT_ON_RESUME {
        setstepcallback(STEP_CB_SECRET_BASE)
    }
}

script SecretBase_EventScript_PC {
    lockall
    playse(SE_PC_LOGIN)
    message(SecretBase_Text_BootUpPC)
    dofieldeffect(FLDEFF_PCTURN_ON)
    waitstate
    waitmessage
    waitbuttonpress
    playse(SE_SELECT)
    goto(SecretBase_EventScript_PCShowMainMenu)
}

script SecretBase_EventScript_PCShowMainMenu {
    message(SecretBase_Text_WhatWouldYouLikeToDo)
    waitmessage
    if (flag(FLAG_SECRET_BASE_REGISTRY_ENABLED)) {
        multichoice(0, 0, MULTI_BASE_PC_WITH_REGISTRY, 0)
        switch (var(VAR_RESULT)) {
            case 0:
                special(ShowSecretBaseDecorationMenu)
            case 1:
                msgbox(SecretBase_Text_AllDecorationsWillBeReturned, MSGBOX_YESNO)
                if (var(VAR_RESULT) == NO) {
                    goto(SecretBase_EventScript_PCShowMainMenu)
                }
                closemessage
                special(MoveOutOfSecretBase)
                releaseall
            case 2:
                special(ShowSecretBaseRegistryMenu)
            case 3:
            case MULTI_B_PRESSED:
                special(DoSecretBasePCTurnOffEffect)
                closemessage
                releaseall
        }
    } else {
        multichoice(0, 0, MULTI_BASE_PC_NO_REGISTRY, 0)
        switch (var(VAR_RESULT)) {
            case 0:
                special(ShowSecretBaseDecorationMenu)
            case 1:
                msgbox(SecretBase_Text_AllDecorationsWillBeReturned, MSGBOX_YESNO)
                if (var(VAR_RESULT) == NO) {
                    goto(SecretBase_EventScript_PCShowMainMenu)
                }
                closemessage
                special(MoveOutOfSecretBase)
                releaseall
            case 2:
            case MULTI_B_PRESSED:
                special(DoSecretBasePCTurnOffEffect)
                closemessage
                releaseall
        }
    }
}

script SecretBase_EventScript_PCCancel {
    lockall
    goto(SecretBase_EventScript_PCShowMainMenu)
}

script SecretBase_EventScript_RecordMixingPC {
    lockall
    message(SecretBase_Text_BootUpPC)
    playse(SE_PC_LOGIN)
    dofieldeffect(FLDEFF_PCTURN_ON)
    waitstate
    waitmessage
    waitbuttonpress
    playse(SE_SELECT)
    goto(SecretBase_EventScript_PCRegisterMenu)
}

script SecretBase_EventScript_PCRegisterMenu {
    message(SecretBase_Text_WhatWouldYouLikeToDo)
    waitmessage
    multichoice(0, 0, MULTI_REGISTER_MENU, 0)
    switch (var(VAR_RESULT)) {
        case 0:
            special(GetCurSecretBaseRegistrationValidity)
            if (var(VAR_RESULT) == 1) {
                msgbox(SecretBase_Text_AlreadyRegisteredDelete, MSGBOX_YESNO)
                if (var(VAR_RESULT) == NO) {
                    goto(SecretBase_EventScript_PCRegisterMenu)
                }
                msgbox(SecretBase_Text_DataUnregistered, MSGBOX_SIGN)
                special(ToggleCurSecretBaseRegistry)
                special(DoSecretBasePCTurnOffEffect)
            } elif (var(VAR_RESULT) == 2) {
                msgbox(SecretBase_Text_TooManyBasesDeleteSome, MSGBOX_SIGN)
                special(DoSecretBasePCTurnOffEffect)
                closemessage
            } else {
                special(CopyCurSecretBaseOwnerName_StrVar1)
                msgbox(SecretBase_Text_WantToRegisterSecretBase, MSGBOX_YESNO)
                if (var(VAR_RESULT) == NO) {
                    goto(SecretBase_EventScript_PCRegisterMenu)
                }
                msgbox(SecretBase_Text_RegistrationCompleted, MSGBOX_SIGN)
                special(ToggleCurSecretBaseRegistry)
                special(DoSecretBasePCTurnOffEffect)
            }
            releaseall
        case 1:
            special(ShowSecretBaseRegistryMenu)
        case 2:
            msgbox(SecretBase_Text_RegistryInfo)
            goto(SecretBase_EventScript_PCRegisterMenu)
        case 3:
        case MULTI_B_PRESSED:
            special(DoSecretBasePCTurnOffEffect)
            closemessage
            releaseall
    }
}

script SecretBase_EventScript_ShowRegisterMenu {
    lockall
    goto(SecretBase_EventScript_PCRegisterMenu)
}

script SecretBase_EventScript_SandOrnament {
    special(CheckInteractedWithFriendsSandOrnament)
    dofieldeffect(FLDEFF_SAND_PILLAR)
    waitstate
}

script SecretBase_EventScript_ShieldOrToyTV {
    special(InteractWithShieldOrTVDecoration)
    switch (var(VAR_RESULT)) {
        case 0:
            msgbox(SecretBase_Text_BattleTowerShield, MSGBOX_SIGN)
        case 1:
            msgbox(SecretBase_Text_ToyTV, MSGBOX_SIGN)
        case 2:
            msgbox(SecretBase_Text_SeedotTV, MSGBOX_SIGN)
        case 3:
            msgbox(SecretBase_Text_SkittyTV, MSGBOX_SIGN)
    }
}

text SecretBase_Text_SmallIndentInWall {
    "There's a small indent in the wall."
}

text SecretBase_Text_IndentUseSecretPower {
    "There's a small indent in the wall.\p"
    "Use the Secret Power?"
}

text SecretBase_Text_DiscoveredSmallCavern {
    "Discovered a small cavern!"
}

text SecretBase_Text_WantToMakeYourSecretBaseHere {
    "Want to make your Secret Base here?"
}
