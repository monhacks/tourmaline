script EventScript_Questionnaire {
    lockall
    msgbox(Mevent_Text_FillOutQuestionnaire, MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO) {
        releaseall
        return
    }
    setvar(VAR_0x8004, EASY_CHAT_TYPE_QUESTIONNAIRE)
    call(Common_ShowEasyChatScreen)
    lock
    faceplayer
    specialvar(VAR_0x8008, GetMartEmployeeObjectEventId)
    if (var(VAR_0x8004) == 1) {
        if (!flag(FLAG_SYS_POKEDEX_GET) || flag(FLAG_SYS_MYSTERY_EVENT_ENABLE)) {
            applymovement(VAR_0x8008, Common_Movement_FaceDown)
            waitmovement(0)
            msgbox(Mevent_Text_QuestionnaireThankYou)
        } else {
            applymovement(VAR_0x8008, Common_Movement_FaceDown)
            waitmovement(0)
            playse(SE_PIN)
            applymovement(VAR_0x8008, Common_Movement_ExclamationMark)
            waitmovement(0)
            applymovement(VAR_0x8008, Common_Movement_Delay48)
            waitmovement(0)
            msgbox(Mevent_Text_YouKnowThoseWordsEvent)
            setflag(FLAG_SYS_MYSTERY_EVENT_ENABLE)
            msgbox(Mevent_Text_YouCanAccessMysteryEvent)
        }
        releaseall
    } elif (var(VAR_0x8004) == 2) {
        if (!flag(FLAG_SYS_POKEDEX_GET) || flag(FLAG_SYS_MYSTERY_GIFT_ENABLE)) {
            applymovement(VAR_0x8008, Common_Movement_FaceDown)
            waitmovement(0)
            msgbox(Mevent_Text_QuestionnaireThankYou)
        } else {
            applymovement(VAR_0x8008, Common_Movement_FaceDown)
            waitmovement(0)
            playse(SE_PIN)
            applymovement(VAR_0x8008, Common_Movement_ExclamationMark)
            waitmovement(0)
            applymovement(VAR_0x8008, Common_Movement_Delay48)
            waitmovement(0)
            msgbox(Mevent_Text_YouKnowThoseWordsGift)
            setflag(FLAG_SYS_MYSTERY_GIFT_ENABLE)
            msgbox(Mevent_Text_YouCanAccessMysteryGift)
        }
        releaseall
    } elif (var(VAR_RESULT) == 0) {
        release
    } elif (var(VAR_RESULT) == 1) {
        applymovement(VAR_0x8008, Common_Movement_FaceDown)
        waitmovement(0)
        msgbox(Mevent_Text_QuestionnaireThankYou)
        releaseall
    }
}
