script EventScript_PC {
    lockall
    setvar(VAR_0x8004, 0)
    special(DoPCTurnOnEffect)
    playse(SE_PC_ON)
    msgbox(Text_BootUpPC)
    goto(EventScript_PCMainMenu)
}

script EventScript_PCMainMenu {
    message(gText_WhichPCShouldBeAccessed)
    waitmessage
    special(ScriptMenu_CreatePCMultichoice)
    waitstate
    goto(EventScript_AccessPC)
}

script EventScript_AccessPC {
    switch (var(VAR_RESULT)) {
        case 0:
            playse(SE_PC_LOGIN)
            if (flag(FLAG_SYS_PC_LANETTE)) {
                msgbox(gText_AccessedLanettesPC)
            } else {
                msgbox(gText_AccessedSomeonesPC)
            }
            msgbox(gText_StorageSystemOpened)
            special(ShowPokemonStorageSystemPC)
            waitstate
            goto(EventScript_PCMainMenu)
        case 1:
            playse(SE_PC_LOGIN)
            msgbox(gText_AccessedPlayersPC)
            special(PlayerPC)
            waitstate
            goto(EventScript_PCMainMenu)
        case 2:
            if (!flag(FLAG_SYS_GAME_CLEAR)) {
                goto(EventScript_TurnOffPC)
            }
            playse(SE_PC_LOGIN)
            special(AccessHallOfFamePC)
            waitstate
            goto(EventScript_AccessPC)
        case 3:
        case MULTI_B_PRESSED:
            goto(EventScript_TurnOffPC)
    }
}

script EventScript_TurnOffPC {
    setvar(VAR_0x8004, 0)
    playse(SE_PC_OFF)
    special(DoPCTurnOffEffect)
    releaseall
}
